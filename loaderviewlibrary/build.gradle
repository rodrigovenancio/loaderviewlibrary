apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.elyeproj.libraries'
version = '1.5.0'

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 18
        versionName "1.5.5"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:27.1.1'
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs

bintray {
    user = 'YOUR_USERNAME'
    key = 'YOUR_API_KEY'
    pkg {
        repo = 'maven'
        name = 'com.elyeproj.loaderviewlibrary'

        version {
            name = '1.5.0-LoaderView'
            desc = 'Loader View Widget'
            released  = new Date()
            vcsTag = '1.5.0'
        }

        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/elye/loaderviewlibrary.git'
        websiteUrl = 'https://github.com/elye/loaderviewlibrary'
    }
    configurations = ['archives']
}

artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}
